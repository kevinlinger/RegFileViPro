////Cell name: buf
subckt buf IN OUT VDD VSS
parameters wp2=wdef wp1=wdef wn2=wdef wn1=wdef l=ldef
MP1 (OUTB IN VDD VDD) P_TRANSISTOR width=wp1 length=l
MN1 (OUTB IN VSS VSS) N_TRANSISTOR width=wn1 length=l
MP2 (OUT OUTB VDD VDD) P_TRANSISTOR width=wp2 length=l
MN2 (OUT OUTB VSS VSS) N_TRANSISTOR width=wn2 length=l
ends buf

// Cell name: bitcell6T
// View name: schematic
subckt bitcell6T BL BLB VDDC VSSC WL
M3 (QB WL BLB VSSC) PG_TRANSISTOR width=wpg length=lpg 
M2 (Q WL BL VSSC) PG_TRANSISTOR width=wpg length=lpg 
M4 (QB Q VSSC VSSC) PD_TRANSISTOR width=wpd length=lpd 
M1 (Q QB VSSC VSSC) PD_TRANSISTOR width=wpd length=lpd 
M5 (QB Q VDDC VDDC) PU_TRANSISTOR width=wpu length=lpu 
M0 (Q QB VDDC VDDC) PU_TRANSISTOR width=wpu length=lpu 
ends bitcell6T

// Cell name: DFF
// View name: schematic
subckt DFF CLK D Q VDD VSS
M25 (Q X3 VSS VSS) N_TRANSISTOR width=1.95*wdef length=ldef mult=1
M23 (X4 X3 VSS VSS) N_TRANSISTOR width=wdef length=ldef mult=1
M21 (A5 X4 VSS VSS) N_TRANSISTOR width=wdef length=ldef mult=1
M20 (X3 CPN A5 VSS) N_TRANSISTOR width=wdef length=ldef mult=1
M17 (X2 CPI X3 VSS) N_TRANSISTOR width=1.35*wdef length=ldef mult=1
M15 (X2 X1 VSS VSS) N_TRANSISTOR width=1.35*wdef length=ldef mult=1
M13 (X1 X0 VSS VSS) N_TRANSISTOR width=wdef length=ldef mult=1
M11 (A3 X1 VSS VSS) N_TRANSISTOR width=wdef length=ldef mult=1
M10 (X0 CPI A3 VSS) N_TRANSISTOR width=wdef length=ldef mult=1
M7 (A1 D VSS VSS) N_TRANSISTOR width=wdef length=ldef mult=1
M6 (X0 CPN A1 VSS) N_TRANSISTOR width=wdef length=ldef mult=1
M3 (CPI CPN VSS VSS) N_TRANSISTOR width=1.5*wdef length=ldef mult=1
M1 (CPN CLK VSS VSS) N_TRANSISTOR width=1.5*wdef length=ldef mult=1
M24 (Q X3 VDD VDD) P_TRANSISTOR width=5.4*wdef length=ldef mult=1
M22 (X4 X3 VDD VDD) P_TRANSISTOR width=2*wdef length=ldef mult=1
M19 (X3 CPI A4 VDD) P_TRANSISTOR width=2*wdef length=ldef mult=1
M18 (A4 X4 VDD VDD) P_TRANSISTOR width=2*wdef length=ldef mult=1
M16 (X3 CPN X2 VDD) P_TRANSISTOR width=2.7*wdef length=ldef mult=1
M14 (X2 X1 VDD VDD) P_TRANSISTOR width=2.7*wdef length=ldef mult=1
M12 (X1 X0 VDD VDD) P_TRANSISTOR width=3*wdef length=ldef mult=1
M9 (X0 CPN A2 VDD) P_TRANSISTOR width=2*wdef length=ldef mult=1
M8 (A2 X1 VDD VDD) P_TRANSISTOR width=2*wdef length=ldef mult=1
M5 (X0 CPI A0 VDD) P_TRANSISTOR width=3.6*wdef length=ldef mult=1
M4 (A0 D VDD VDD) P_TRANSISTOR width=3.6*wdef length=ldef mult=1
M2 (CPI CPN VDD VDD) P_TRANSISTOR width=3*wdef length=ldef mult=1
M0 (CPN CLK VDD VDD) P_TRANSISTOR width=3*wdef length=ldef mult=1 
ends DFF
// End of subcircuit definition.

//Inverter subcircuit
subckt INV OUT IN VDD VSS
parameters i
MP (OUT IN VDD VDD) P_TRANSISTOR width=2*i*wdef length=ldef
MN (OUT IN VSS VSS) N_TRANSISTOR width=i*wdef length=ldef
ends INV
// End of subcircuit definition.

// Cell name: triinv
// View name: schematic
subckt triinv IN OUT VDD VSS TRI_N TRI_P
parameters wp=4*wdef lp=ldef wn=2*wdef ln=ldef
M0 (OUT IN net19 VDD) P_TRANSISTOR width=wp length=lp 
MP (net19 TRI_P VDD VDD) P_TRANSISTOR width=wp length=lp 
M1 (OUT IN net31 VSS) N_TRANSISTOR width=wn length=ln 
MN (net31 TRI_N VSS VSS) N_TRANSISTOR width=wn length=ln 
ends triinv
// End of subcircuit definition.

// Cell name: IO
// View name: schematic
subckt IO DIN DOUT ICLK NRDWR OCLK RDWR SD VDD VSS WEN
    I0 (OCLK SD DOUT VDD VSS) DFF
    I1 (ICLK DIN d VDD VSS) DFF
    I10 (d db VDD VSS) inv m=1 wp=8*wdef lp=ldef wn=4*wdef ln=ldef
    I13 (WEN NWEN VDD VSS) inv m=1 wp=8*wdef lp=ldef wn=4*wdef ln=ldef
    I14 (NWEN WENB VDD VSS) inv m=1 wp=8*wdef lp=ldef wn=4*wdef ln=ldef
    I16 (d NRDWR VDD VSS WENB NWEN) triinv wp=2*wnio lp=ldef wn=wnio ln=ldef
    I15 (db RDWR VDD VSS WENB NWEN) triinv wp=2*wnio lp=ldef wn=wnio ln=ldef
ends IO
// End of subcircuit definition.

// Cell name: nand2
// View name: schematic
subckt nand2 INbot INtop OUT VDD VSS
parameters wp=2*wdef lp=ldef wn=2*wdef ln=ldef
M2 (OUT INtop VDD VDD) P_TRANSISTOR width=wp length=lp 
MP (OUT INbot VDD VDD) P_TRANSISTOR width=wp length=lp 
M1 (OUT INtop net048 VSS) N_TRANSISTOR width=wn length=ln 
M3 (net048 INbot VSS VSS) N_TRANSISTOR width=wn length=ln 
ends nand2
// End of subcircuit definition.

// Cell name: SA
// View name: schematic
subckt SA NRDWR RDWR SAE SAPREC SD VDD VSS
I5 (SD OUT SDB VDD VSS) nand2 wp=wdef lp=ldef wn=wdef ln=ldef
I34 (SDB OUTB SD VDD VSS) nand2 wp=wdef lp=ldef wn=wdef ln=ldef
M19 (COM SAE VSS VSS) N_TRANSISTOR width=wdef length=ldef 
M11 (xinb NRDWR COM VSS) N_TRANSISTOR width=wdef length=ldef 
M10 (xin RDWR COM VSS) N_TRANSISTOR width=wdef length=ldef 
M8 (OUTB OUT xin VSS) N_TRANSISTOR width=3*wdef length=ldef 
M7 (OUT OUTB xinb VSS) N_TRANSISTOR width=3*wdef length=ldef 
M20 (NRDWR SAPREC VDD VDD) P_TRANSISTOR width=5*wdef length=ldef
M24 (xinb SAE VDD VDD) P_TRANSISTOR width=wdef length=ldef 
M23 (xin SAE xinb VDD) P_TRANSISTOR width=wdef length=ldef 
M25 (xin SAE VDD VDD) P_TRANSISTOR width=wdef length=ldef 
M14 (RDWR SAPREC VDD VDD) P_TRANSISTOR width=5*wdef length=ldef
M5 (OUT OUTB VDD VDD) P_TRANSISTOR width=wdef length=ldef
M4 (OUTB OUT VDD VDD) P_TRANSISTOR width=wdef length=ldef 
M3 (OUTB SAE OUT VDD) P_TRANSISTOR width=wdef length=ldef 
M2 (OUT SAE VDD VDD) P_TRANSISTOR width=wdef length=ldef 
M1 (OUTB SAE VDD VDD) P_TRANSISTOR width=wdef length=ldef 
ends SA
// End of subcircuit definition.

// Cell name: col mux
// View name: schematic
subckt col_mux BL BLB CSEL CSELB NRDWR RDWR VDD VSS
parameters wncs=wdef lncs=ldef wpcs=wdef lpcs=ldef
M3 (BLB CSEL NRDWR VSS) N_TRANSISTOR width=wncs length=lncs 
M1 (BL CSEL RDWR VSS) N_TRANSISTOR width=wncs length=lncs 
M4 (NRDWR CSELB BLB VDD) P_TRANSISTOR width=wpcs length=lpcs 
M2 (RDWR CSELB BL VDD) P_TRANSISTOR width=wpcs length=lpcs 
ends col_mux

//generic inverter chain optimize later
subckt INVCHAIN IN OUT VDD VSS
I0 (X1 IN VDD VSS) INV i=1
I1 (X2 X1 VDD VSS) INV i=4
I2 (X3 X2 VDD VSS) INV i=16
I3 (OUT X3 VDD VSS) INV i=48
ends INVCHAIN

VBL (BL 0) vsource dc=pvdd-BL_DIFF
VBLB (BLB 0) vsource dc=pvdd*0.93

//BC and CD
ICOL1 (BL BLB CSEL CSELB NRDWR RDWR VDD VSS) col_mux

//SA
ISA (NRDWR RDWR SAE SAPREC SD VDD VSS) SA 

// capacitance of SA driving the tristate buffer (hardcoded for now assuming pmos 16x min, nmos 8x min)
cap1 (SD 0) capacitor c=wdef*cg*1e6*24

//Input drivers for DUT
VVDD (VDD 0) vsource dc=pvdd
VVSS (VSS 0) vsource dc=0

VCSEL (CSEL_IN 0) vsource type=pulse val0=0 val1=pvdd rise=trf fall=trf width=tper/2.5 period=tper delay=1n delay=tdly
VCSELB (CSELB_IN 0) vsource type=pulse val0=pvdd val1=0 rise=trf fall=trf width=tper/2.5 period=tper delay=1n delay=tdly

ICSEL (CSEL_IN CSEL VDD VSS) INVCHAIN
ICSELB (CSELB_IN CSELB VDD VSS) INVCHAIN
ISAE (SAE_IN SAE VDD VSS) INVCHAIN
ISAPREC (SAPREC_IN SAPREC VDD VSS) INVCHAIN

VSAE (SAE_IN 0) vsource type=pulse val0=0 val1=pvdd rise=trf fall=trf width=tper/5 period=tper delay=2n
VSAPREC (SAPREC_IN 0) vsource type=pulse val0=0 val1=pvdd rise=trf fall=trf width=tper/2.5 period=tper delay=1n delay=tdly

// CAPS assume that banks placed side by side and signals generated from middle of structure
// wire cap + gate cap for each component
capSAE (SAE 0) capacitor c=numBanks/2*ws*cwl*colMux+wdef*cg*1e6*(ws-1)*7 
capSAPREC (SAPREC 0) capacitor c=numBanks/2*ws*cwl*colMux+wdef*cg*1e6*(ws-1)*10
capCSEL (CSEL 0) capacitor c=numBanks/2*ws*cwl*colMux+wdef*cg*1e6*(ws-1)*2
capCSELB (CSELB 0) capacitor c=numBanks/2*ws*cwl*colMux+wdef*cg*1e6*(ws-1)*2

ic ISA.OUT=pvdd ISA.OUTB=pvdd ISA.SD=pvdd ISA.SDB=0 
myOption options pwr=all pivotdc=yes
