// ========================================================
//                         Title
// ========================================================
simulator lang=spectre
myOption options pwr=all

// ========================================================
//                      Subcircuits Used
// ========================================================

// Subcircuit: inverter
subckt inverter VDD VSS In Out
parameters wn=wdef wp=2*wdef ln=ldef lp=ldef mult=1 alpha=1
   P0 (Out In VDD VDD) P_TRANSISTOR width=alpha*wp length=lp m=mult 
   N0 (Out In VSS VSS) N_TRANSISTOR width=alpha*wn length=ln m=mult
ends inverter
// End of subcircuit definition.

// Subcircuit: NAND2
subckt NAND2 VDD VSS inA inB out
parameters wn=2*wdef ln=ldef wp=2*wdef lp=ldef alpha=1 mult=1 
    PA (out inA VDD VDD) P_TRANSISTOR width=wp*alpha length=lp m=mult
    PB (out inB VDD VDD) P_TRANSISTOR width=wp*alpha length=lp m=mult
    NA (out inA netAB VSS) N_TRANSISTOR width=wn*alpha length=ln m=mult
    NB (netAB inB VSS VSS) N_TRANSISTOR width=wn*alpha length=ln m=mult
ends NAND2
// End of subcircuit definition

// Subcircuit: parasitics
subckt parasitics VSS in out
parameters capacitance resistance 
    C0 (in VSS) capacitor c=capacitance/2
    R0 (in out) resistor r=resistance
    C1 (out VSS) capacitor c=capacitance/2
ends parasitics
// End of subcircuit definition

// Subcircuit: bitcell
subckt bitcell VDD VSS WL BL BLB
parameters mult=1
    N5 (BL WL Q VSS) N_TRANSISTOR width=wpg length=lpg m=mult
    P4 (QB Q VDD VDD) P_TRANSISTOR width=wpu length=lpu m=mult
    N3 (QB Q VSS VSS) N_TRANSISTOR width=wpd length=lpd m=mult
    P2 (Q QB VDD VDD) P_TRANSISTOR width=wpu length=lpu m=mult
    N1 (Q QB VSS VSS) N_TRANSISTOR width=wpd length=lpd m=mult
    N6 (BLB WL QB VSS) N_TRANSISTOR width=wpg length=lpg m=mult
ends bitcell
// End of subcircuit definition

// Subcircuit: buffer
subckt buffer VDD VSS in out
parameters wn0=wdef wp0=2*wdef ln0=ldef lp0=ldef mult0=1 alpha0=1 alpha1=1
    I0 (VDD VSS in inbar) inverter wn=wn0 ln=ln0 wp=wp0 lp=lp0 alpha=alpha0 mult=mult0
    I1 (VDD VSS inbar out) inverter wn=wn0 ln=ln0 wp=wp0 lp=lp0 alpha=alpha1 mult=mult0
ends buffer
// End of subcircuit definition.

// Subcircuit: twobuffers
subckt twobuffers VDD VSS in out
parameters wn0=wdef wp0=2*wdef ln0=ldef lp0=ldef mult0=1 alpha0=1 alpha1=1 alpha2=1 alpha3=1
    I0 (VDD VSS in inbar) inverter wn=wn0 ln=ln0 wp=wp0 lp=lp0 alpha=alpha0 mult=mult0
    I1 (VDD VSS inbar inbarbar) inverter wn=wn0 ln=ln0 wp=wp0 lp=lp0 alpha=alpha1 mult=mult0
    I2 (VDD VSS inbarbar inbarbarbar) inverter wn=wn0 ln=ln0 wp=wp0 lp=lp0 alpha=alpha2 mult=mult0
    I3 (VDD VSS inbarbarbar out) inverter wn=wn0 ln=ln0 wp=wp0 lp=lp0 alpha=alpha3 mult=mult0
ends twobuffers
// End of subcircuit definition.

// Subcircuit: predecoder5
subckt predecoder5 VDD VSS in out
    Nand0 (VDD VSS in VDD postN0) NAND2 mult=2
    Inv0 (VDD VSS postN0 preN1) inverter
    Nand1 (VDD VSS preN1 VDD postN1) NAND2 mult=2
    Inv1 (VDD VSS postN1 out) inverter
ends predecoder5
// End of subcircuit definition.

// Subcircuit: nobuffers
subckt nobuffers VDD VSS in out
    R0 (in out) resistor r=0.000000000000001
ends nobuffers
// End of subcircuit definition.

// Subcircuit: wldriver
subckt wldriver VDD VSS in out
    Nand3 (VDD VSS in VDD pre_buf) NAND2 mult=1
    I0 (VDD VSS pre_buf pre_buf1) inverter alpha=4
    I1 (VDD VSS pre_buf1 pre_wl) inverter alpha=16
    //Kbuffer0 (VDD VSS pre_buf pre_wl) twobuffers alpha0=1 alpha1=4 alpha2=16 alpha3=64
    Inv1 (VDD VSS delay18 delay18B) inverter
    N1 (pre_wl delay18B out VSS) N_TRANSISTOR width=16*wdef length=ldef
    P1 (pre_wl delay18 out VDD) P_TRANSISTOR width=16*wdef length=ldef 
    buf1 (VDD VSS pre_wl delay1) buffer
    buf2 (VDD VSS delay1 delay2) buffer
    buf3 (VDD VSS delay2 delay3) buffer
    buf4 (VDD VSS delay3 delay4) buffer
    buf5 (VDD VSS delay4 delay5) buffer
    buf6 (VDD VSS delay5 delay6) buffer
    buf7 (VDD VSS delay6 delay7) buffer
    buf8 (VDD VSS delay7 delay8) buffer
    buf9 (VDD VSS delay8 delay9) buffer
    buf10 (VDD VSS delay9 delay10) buffer
    buf11 (VDD VSS delay10 delay11) buffer
    buf12 (VDD VSS delay11 delay12) buffer
    buf13 (VDD VSS delay12 delay13) buffer
    buf14 (VDD VSS delay13 delay14) buffer
    buf15 (VDD VSS delay14 delay15) buffer
    buf16 (VDD VSS delay15 delay16) buffer
    buf17 (VDD VSS delay16 delay17) buffer
    buf18 (VDD VSS delay17 delay18) buffer
    buf19 (VDD VSS delay18 delay19) buffer
    buf20 (VDD VSS delay19 delay20) buffer
    C0 (out delay18) capacitor c=wpg*2*cg*1e6*multiplier/8
    C1 (out delay20) capacitor c=wpg*2*cg*1e6*multiplier/8
    
ends wldriver
// End of subcircuit definition.


//Subcircuit: WCpathN0K0
//critical path for 5 bits when n = 0 and k = 0
subckt WCpathN0K0 VDD VSS in out
parameters wirecap wireres
    Inv0 (VDD VSS in preN0) inverter
    Predecoder0 (VDD VSS preN0 preNbuf) predecoder5
    Nbuffer0 (VDD VSS preNbuf prePAR) nobuffers
    PAR0 (VSS prePAR preN2) parasitics capacitance = wirecap resistance = wireres
    Nand2 (VDD VSS preN2 VDD postN2) NAND2 mult=8
    Inv2 (VDD VSS postN2 preWL) inverter
    WLdriver0 (VDD VSS preWL preKbuf) wldriver
    Kbuffer0 (VDD VSS preKbuf out) nobuffers
ends WCpathN0K0
// End of Subcircuit Definition



// ========================================================
//                      Test Bench
// ========================================================

//power supplies
V0 (vdd_0 0) vsource dc=pvdd type=dc	//voltage source for 5 bit model with n = 0 and k = 0

VGLOBAL (vdd 0) vsource dc=pvdd type=dc	//global voltage source

//input signals (goes from 1 -> 0)
VVIN0 ( in_0 0 ) vsource type=pulse val0=0 val1=pvdd delay=10n rise=0.01n fall=0.01n width=16n period=32n 

//BUFFERS TO MAKE INPUT SIGNALS MORE REALISTIC

B0 (vdd 0 in_0 in0) buffer alpha0=1 alpha1=1

//DUT(S)

DUT0 (vdd_0 0 in0 WL0) WCpathN0K0 wirecap=wireCap wireres=wireRes

//BITCELLS

Bitcell0 (vdd 0 WL0 vdd vdd) bitcell mult=multiplier




