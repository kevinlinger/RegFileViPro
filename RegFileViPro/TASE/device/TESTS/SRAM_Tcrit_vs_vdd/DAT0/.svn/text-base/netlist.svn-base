//============================================
// Define VDD and VSS
//============================================
VVDD (VDD 0) vsource dc=pvdd
VVSS (VSS 0) vsource dc=0

//============================================
// Half cell
//============================================
subckt HALFCELL (IN OUT BL WL VDD VBP VSS VBN)
  MP (OUT IN VDD VBP) PU_TRANSISTOR width=wpu length=lpu
  MN (OUT IN VSS VBN) PD_TRANSISTOR width=wpd length=lpd
  MT (BL WL OUT VBN) PG_TRANSISTOR width=wpg length=lpg
ends HALFCELL

//============================================
// BITCELL
//============================================
ICellL (QB Q BL WL VDD VBP VSS VBN) HALFCELL
ICellR (Q QB BLB WL VDD VBP VSS VBN) HALFCELL

ic Q=0 QB=pvdd

VWL (WL 0) vsource type=pulse val0=0 val1=pvdd rise=pr fall=pf width=pw

VBL (BL 0) vsource dc=pvdd
VBLB (BLB 0) vsource dc=0

VVBP (VBP 0) vsource dc=pvbp
VVBN (VBN 0) vsource dc=0
