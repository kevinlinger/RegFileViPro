subckt bitcell VSS VDD BL WL
    P0 (Q QB VDD VDD) P_TRANSISTOR width=wdef length=ldef
    N0 (Q QB VSS VSS) N_TRANSISTOR width=wdef length=ldef

    P1 (QB Q VDD VDD) P_TRANSISTOR width=wdef length=ldef
    N1 (QB Q VSS VSS) N_TRANSISTOR width=wdef length=ldef

    N2 (BL WL Q VSS) N_TRANSISTOR width=wdef length=ldef
ends bitcell

subckt NSRSA  VSS VDD OUT BL PRE PREB
    // devices for first inverter
    P0 (X   IN   VDD VDD) P_TRANSISTOR width=wdef length=ldef
    N0 (X   IN   VSS VSS) N_TRANSISTOR width=wdef length=ldef
    S0 (X   PREB IN  VDD) P_TRANSISTOR width=wdef length=ldef
    C0 (BL  IN) capacitor c=1f

    // devices for second inverter
    P1 (Y   INX  VDD VDD) P_TRANSISTOR width=wdef length=ldef
    N1 (Y   INX  VSS VSS) N_TRANSISTOR width=wdef length=ldef
    S1 (Y   PRE  INX VDD) N_TRANSISTOR width=wdef length=ldef
    C1 (X   INX) capacitor c=1f

    // feedback devices
    N2 (IN  X    W   VSS) N_TRANSISTOR width=wdef length=ldef
    N3 (Y   PREB W   VSS) N_TRANSISTOR width=wdef length=ldef

    // output pre-charge & feedback
    P4 (OUB PREB VDD VDD) P_TRANSISTOR width=wdef length=ldef
    N4 (OUB X    Q   VSS) N_TRANSISTOR width=wdef length=ldef
    N5 (Y   PREB Q   VSS) N_TRANSISTOR width=wdef length=ldef

    // output buffer
    P6 (OUT OUB  VDD VDD) P_TRANSISTOR width=wdef length=ldef
    N6 (OUT OUB  VSS VSS) N_TRANSISTOR width=wdef length=ldef
ends NSRSA

// Main Circuit

// Bitcell column
B0 (VSS VDD BL WL) bitcell
B1 (VSS VDD BL VSS) bitcell m=255
C0 (BL 0) capacitor c=25f // capacitance of metal

// Decouple BL from input
NX (IN WL BL VSS) N_TRANSISTOR width=wdef length=ldef

// SA
N1 (VSS VDD OUT BL PRE PREB) NSRSA

// Precharge
P0 (BL PREB VDD VDD) P_TRANSISTOR width=wdef length=ldef

VPRE (PRE 0) vsource type=pulse val0=pvdd val1=0.0  delay=0n rise=0.2n fall=0.2n width=1.8n period=4n
VPREB (PREB 0) vsource type=pulse val0=0.0  val1=pvdd delay=0n rise=0.2n fall=0.2n width=1.8n period=4n 

VWL (WL 0) vsource type=pwl wave=[0n 0 5n 0 5.2n pvdd 6n pvdd 6.2n 0]

VVDD (VDD 0) vsource dc=pvdd
VVSS (VSS 0) vsource dc=0

ic B0.Q=0 B0.QB=pvdd B1.Q=pvdd B1.QB=0
