//============================================
// RST Bitcell
//============================================
subckt bitcell5TRST BL RRST VBN VBP VDD VSS WL CRST
MPR (QB Q VDD VBP) P_TRANSISTOR width=wp2 length=lp2
MPL (Q QB VDD VBP) P_TRANSISTOR width=wp1 length=lp1
MNR (QB Q VSS VBN) N_TRANSISTOR width=wn2 length=ln2
MNL (Q QB VSS VBN) N_TRANSISTOR width=wn1 length=ln1
MTR (RRST CRST QB VBN) N_TRANSISTOR  width=wrs length=lrs
MTL (BL WL Q VBN) N_TRANSISTOR  width=wna length=lna
ends bitcell5TRST

//============================================
// 6T Bitcell
//============================================
subckt bitcell6TB BL BLB WL VDD VBP VSS VBN
MPR (QB Q VDD VBP) P_TRANSISTOR width=wpu length=lpu
MPL (Q QB VDD VBP) P_TRANSISTOR width=wpu length=lpu
MNR (QB Q VSS VBN) N_TRANSISTOR width=wpd length=lpd
MNL (Q QB VSS VBN) N_TRANSISTOR width=wpd length=lpd
MTL (BL WL Q VBN) N_TRANSISTOR  width=wpg length=lpg
MTR (BLB WL QB VBN) N_TRANSISTOR  width=wpg length=lpg
ends bitcell6TB

//-------------------------------------------------------
//RST bitcells
IA5T (BL1 VDD VBN VBP VDD VSS WLA5T 0) bitcell5TRST
ID5T1 (BL1 VDD VBN VBP VDD VSS WLU1 0) bitcell5TRST m=numRows-1
IREF1 (BL1 VDD VBN VBP VDD VSS WLR1 0) bitcell5TRST
C5T1 (BL1 0) capacitor c=cap5

IREF2 (BL2 VDD VBN VBP VDD VSS WLR2 0) bitcell5TRST
ID5T2 (BL2 VDD VBN VBP VDD VSS WLU2 0) bitcell5TRST m=numRows
C5T2 (BL2 0) capacitor c=cap5
//--------------------------------------------------------

//--------------------------------------------------------
// 6T bitcells
IA6T (BL BLB WLA6T VDD VBP VSS VBN) bitcell6TB
ID6T (BL BLB WLU VDD VBP VSS VBN) bitcell6TB m=2*numRows+1
C6T1 (BL 0) capacitor c=cap6
C6T2 (BLB 0) capacitor c=cap6
//--------------------------------------------------------

VVDD (VDD 0) vsource dc=pvdd
VVBP (VBP 0) vsource dc=pvdd
VVBN (VBN 0) vsource dc=0
VVSS (VSS 0) vsource dc=0

VWLA5 (WLA5T 0) vsource type=pulse val0=0 val1=pvdd rise=pr fall=pf width=pw
VWLU1 (WLU1 0) vsource dc=0
VWLR1 (WLR1 0) vsource dc=0
VWLR2 (WLR2 0) vsource type=pulse val0=0 val1=pvddl rise=pr fall=pf width=pw
VWLU2 (WLU2 0) vsource dc=0

VWLA6 (WLA6T 0) vsource type=pulse val0=0 val1=pvdd rise=pr fall=pf width=pw
VWLU (WLU 0) vsource dc=0
